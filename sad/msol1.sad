 ON ECHO;OFF CTIME; 
 MOMENTUM= 0.299792458 GEV; 
 ;
 LINE RING = (QDUM 4*B)
      MSOL=( PEXT ZV 21*RING  PINJ)
 ;
 QUAD QDUM=()
 ;
 MULT B = (L = PI/2 ANGLE = PI/2 K1 = -0.001 K3 = -0.3)
      ZV = (SK0 = 0) 
 ;
 MARK   PEXT =(BETAX = 1      BETAY =39.6    EMIX = 1E-6
   EMIY = 1E-6  DP = 0.01 SIGZ = 1E-3 )
        PINJ =()
 ;

 ;
 DRIFT  LA1     =(L =.7994033775705999 )   LA2     =(L =.46308059741153396 )
        LA3     =(L =1.0589976554610525 )  LA4     =(L =.8165056152743786 )
        LA5     =(L =.9770260459083815 )
 ;
 QUAD   QD1     =(L =.1   K1 =-.8770727025308328
           ROTATE =-29.07270068121177 DEG )
        QF1     =(L =.1   K1 =1.9317697977921202
           ROTATE =-26.022704251437165 DEG )
        QD2     =(L =.1   K1 =1.885208300574978
           ROTATE =-20.472990885870075 DEG )
        QF2     =(L =.1   K1 =.5491398971758901
           ROTATE =-9.630946940294272 DEG )
        QD3     =(L =.1   K1 =.9571683702467455
           ROTATE =3.70432910915698 DEG )
        QF3     =(L =.1   K1 =-1.7538179061843115
           ROTATE =1.8848021980078746 DEG )
 ;
 MAP    MMAP                            =()
 ;
 MARK   PEXT    =(BX =1   AY =15.832197535455938   BY =3999.9706910612026
           DP =.01   SIGZ =.001     EMITX =1e-06   EMITY =1e-06 )
        PLIN    =(AX =-5.84583732532451e-08   BX =3.0000000093798795
           PSIX =4.122911445458927  AY =-5.161325318692178e-05
           BY =2.999906330678273    PSIY =3.2032994384735085
           EX =-.5149800801591705   EPX =-.032684694603617316
           EY =10.48149599341103    EPY =-12.830627378511311
           R1 =-2.7080822879364372e-08   R2 =1.7084928100962757e-07
           R3 =3.260922943882393e-08     R4 =-2.041549150886408e-07
           DP =.01   SIGZ =.001     EMITX =1e-06   EMITY =1e-06 )
 ;
 ;
 LINE MATCH = (PEXT MMAP QD1 LA1 QF1 LA2
               QD2 LA3 QF2 LA4 QD3 LA5 QF3 PLIN)
      MINJ = (-MATCH -MSOL)
 ;
 FFS USE=MSOL;
  CELL;

  CALC;

  draw bx by & ex ey & dx dy q*;

  {bx0, by0} = Twiss[{"BX", "BY"}, "^^^"];

  INS;

  Element["SK0","ZV"]=0.003;

  CALC;

  draw bx by & ex ey & dx dy q*;

  scale = 1e-6;
  dq = {dx, dpx, dy, dpy, dz, dp} = 
    {1, 1/bx0, 1, 1/by0, 1, 0.1}*scale;

  q = IdentityMatrix[6]*dq;
  q = MapThread[Join, {q,-q}];

  AppendTo[q, Table[1, {12}]];

  qt={1,q};

  qt1 = TrackParticles[qt, "$$$"];

  dqt = Subtract@@Partition[#, 6]&/@Drop[qt1[[2]], -1];

  m = (# / dq / 2)& /@ dqt;

  Standardform[$FORM="12.5";
    Print/@m];

  mi = -SymplecticJ[6].Transpose[m].SymplecticJ[6];

  mim = mi.m;

  StandardForm[$FORM="8.4";PageWidth=80;
    Print[mim]];

  Symplectize[m0_]:=Module[{m=m0,d=1,mi,dm},
    While[d > 1e-11,
      Print[d];
      mi = -SymplecticJ[6].Transpose[m].SymplecticJ[6];
      dm = (IdentityMatrix[6] - mi.m)/2;
      m = m + m.dm;
      d = Max[Abs[dm]]];
    m];

  ms = Symplectize[m];

  NOCOD; NOEMIT;
  {mcalc, dcod} = {OneTurnTransferMatrix, OrbitAtExit}/.
    Emittance[OneTurnInformation->True];
  StandardForm[$FORM="8.4";PageWidth=80;
    Print[ms];
    Print[mcalc]];

 USE MATCH;

 ExternalMap["OPTICS",LINE["POSITION","MMAP"],cod_]:=
   {Table[0,{6}], ms};

 adjustrot[]:=Module[{
   quads = Element["POSITION", "Q*"], theta, k1},
   theta = Element["ROTATE", quads];
   k1    = Element["K1",     quads];
   k1c = k1*Cos[2 theta];
   k1s = k1*Sin[2 theta];
   k1 = Abs[k1]*Sign[k1c];
   theta = ArcTan[k1s/k1c]/2;
   MapThread[
     (Element["K1", #] = #2;
      Element["ROTATE", #] = #3)&,
     {quads, k1,theta}];
   ];
 
 adjustrot[];

 dr := FFS["draw bx & by q*;"];

 fit r1 0 r2 0 r3 0 r4 0;
 fit ax 0 ay 0 bx 3 by 3;
 free q* q* rotate l* ayi;
 l* max 1.2;
 
 go

 USE MINJ;

 revrot[]:=Module[{
   quads = Element["POSITION", "Q*"]},
   (Element["ROTATE", #] = -Element["ROTATE", #])&/@quads];

! revrot[];

 dcod[[2]] = -dcod[[2]];
 dcod[[4]] = -dcod[[4]];
 ExternalMap["OPTICS",LINE["POSITION","MMAP"],cod_]:=
   {cod + dcod, IdentityMatrix[6]};

 calc

 ExternalMap["TRACK",LINE["POSITION","MMAP"],_,x_]:=Append[Take[x, 6] + dcod, x[[-1]]];
 Element["SK0","ZV"]=0.003;

 trackemi[np_, emi_, sige_]:=Module[
   {{bx, by} = Twiss[{"BX", "BY"}, "^^^"], p, siz},
   p = GaussRandom[6, np];
   siz = Join[ Sqrt[emi * {bx, 1/bx, by, 1/by}], {0, sige}];
   p *= siz;
   AppendTo[p, Table[1, {np}]];
   TrackParticles[{1, p}, "$$$"][[2]]];

 p1 = trackemi[300, 1e-9, 0];

 part["X"]={1,2};
 part["Y"]={3,4};
 label["X"]={"x (mm)","p`dx`n (10`u-3`n)"};
 label["Y"]={"y (mm)","p`dy`n (10`u-3`n)"};
 colors={"purple","green"};

 plottrack[l_,plane_]:=Module[{p,gy},
   p=trackemi[1000,#,0]&/@l;
   gy=MapThread[ListPlot[1000*Thread[#[[part[plane]]]],
     PointColor->#2,
     PointSize->0.3,
     FrameLabel->label[plane],
     GridLines->{Automatic,Automatic},
     DisplayFunction->Identity,
     AspectRatio->1]&,
     {p, Take[colors, Length[p]]}]];

 gx=plottrack[{1e-10,1e-12},"X"];
 gy=plottrack[{1e-10,1e-12},"Y"];

 Show[{
   Graphics[ Rectangle[{-0.1, 0}, {0.5, 0.7}, gx] ],
   Graphics[ Rectangle[{ 0.5, 0}, {1.1, 0.7}, gy] ]}];,
 Update[];

end

!
! StandardForm like Block environment
!
! Maintainor: A.Morita(4794)
! Revision:   $Id: StandardBlock.n,v 1.3 2009/02/17 05:29:23 amorita Exp $
! Style:      Global Function Library
! Require:    None
!

StandardFFS$=Class[{},
  {
    },
  { FFSstate
    },

  Destructor[] := (Restore[FFSstate]);

  Constructor[] := (FFSstate = Backup[]; Standard[]);

  Standard[] := (
    DP0 = 0; DP = 0.01; CONVERGENCE = 1e-9; MatchingResidual = 0;
    Clear[FitValue, FitWeight, VariableRange, FitFunction];
    FFS["FIX *; REJECT TOTAL"];

    );

  Backup[] := StandardForm[{
    "DP0"->DP0, "DP"->DP, "CONVERGENCE"->CONVERGENCE,
    "MatchingResidual"->MatchingResidual,
    "VARIABLES"->FFS["VARIABLES"], "SHOW"->FFS["SHOW"],
    "FUNCTIONS"->{?FitValue, ?FitWeight, ?VariableRange, ?FitFunction},

    Null[]}];

  Restore[backup_List] := StandardForm[
    Clear[FitValue, FitWeight, VariableRange, FitFunction];

    DP0 = "DP0"/.backup;
    DP  = "DP"/.backup;
    CONVERGENCE      = "CONVERGENCE"/.backup;
    MatchingResidual = "MatchingResidual"/.backup;

    FFS["FIX *;"]; Scan[With[{elm = #[[1]], key = #[[2]],
      min = #[[6]], max = #[[7]], src = #[[8]], coef = #[[9]]},
      FFS["COUPLE "//elm//" "//elm//" 1;"];
      FFS["FREE "//elm//"; "//elm//" MIN "//min//" MAX "//max//";"];
      If[Not[Or[src === "", src === elm, src === elm//".1"]],
	FFS["COUPLE "//elm//" "//src//" "//coef//";"]];
      ]&, "VARIABLES"/.backup];

    FFS["REJECT TOTAL"]; Scan[With[{elm1 = #[[1]], elm2 = #[[2]],
      func = #[[3]], goal = #[[4]], np = #[[5]]},
      FFS[StringJoin@@Prepend[Flatten[Thread[{" ",
	{elm1, elm2, func, goal, np}}]], "FIT"]];
      ]&, "SHOW"/.backup];

    Scan[ReleaseHold, "FUNCTIONS"/.backup];

    ];

  ];

StandardFFS[argv_] := Module[{temporary},
  temporary = StandardFFS$[];
  argv];

SetAttributes[StandardFFS, {HoldAll}];

! End of File
